<?xml version="1.0" encoding="utf-8"?>
<mx:Application xmlns:mx="http://www.adobe.com/2006/mxml" xmlns:Mosaik="com.mosaik.*" xmlns:l="*" xmlns:SAG="com.sag.view.*" layout="absolute" width="100%" height="100%"  backgroundGradientColors="[0x000000,0x323232]" applicationComplete="componentInit();">
	
	<mx:VBox width="100%" height="100%">

		<mx:Label text="Seminar {seminar_id} dauer {dauer} Tag(e) in {standort}" width="100%" opaqueBackground="0xffffff" />
		<mx:HDividedBox width="100%" height="100%">
			<mx:VBox width="100%">
				<SAG:TagesPlanung id="p" textAlign="center" />
			</mx:VBox>
			<mx:VBox height="100%">
				<SAG:Referenten id="referent" />
				<SAG:TagesPlanungTrashcan id="trash" />
			</mx:VBox>
		</mx:HDividedBox>
		<mx:Spacer width="100%" height="5" />
		<mx:ApplicationControlBar width="100%" visible="{ !ExternalInterface.available}"  includeInLayout="{ !ExternalInterface.available }">
			<mx:Spacer width="100%" />
			<mx:Button click="onSaveButtonPressed();" label="Speichern" color="0x000000"/>
		</mx:ApplicationControlBar>
	</mx:VBox>
	
	<mx:Script><![CDATA[
		import com.mosaiksoftware.GMBus;

		import flash.display.LineScaleMode;
		import mx.collections.ArrayCollection;
		import mx.controls.Alert;
		import com.mosaiksoftware.MosaikConfig;
		import com.mosaiksoftware.JsonProxy;
		import mx.rpc.events.ResultEvent;
		import mx.rpc.events.FaultEvent;
		import com.sag.models.PlanungTag;
		
		
		import flash.external.ExternalInterface;


		/* Configuration 
		 * init for MosaikConfig made here! */
//		MosaikConfig.setVar("version", "0.1");
	
		private var seminar:Object;
		
		[Bindable]
		private	var tage:Array;

		[Bindable]
		private var seminar_id:String = "#seminar_id#";
		[Bindable] 
		private var dauer:String = "#dauer#";
		[Bindable]
		private var standort:String = "#standort#";
		
		private var standort_id:String;
		
		private var seminarLoader:JsonProxy = null;
		private var standortLoader:JsonProxy = null;
		private var planungLoader:JsonProxy = null;

		private var _isReady:Boolean = false;
		private var _token:String = "";



		
		public function onSaveButtonPressed():void {
			var saveTage:Object = new Object;
			
			saveTage.mode = "multi";
			saveTage.standort_id = standort_id;
			saveTage.data = new Object();
			saveTage.seminar_id = seminar_id;
			
			for ( var i:String in p.tage ) {
				saveTage.data[i] = p.tage[i].data.toArray();
			}
			planungLoader.save(saveTage);
		}
		
		private function componentInit():void {		
			MosaikConfig.setVar("datahost", "");
			if ( ExternalInterface.available ) {
				//ExternalInterface.marshallExceptions = true;
				ExternalInterface.addCallback("queryService", this.queryService);
				ExternalInterface.addCallback( "isReady", this.isReady);
				ExternalInterface.addCallback( "save", this.onSaveButtonPressed);
			} else {
				seminar_id = "UVV";
				standort_id = "1";
			}
			_isReady = true;
		}
		
		public function isReady():Boolean {
			return _isReady;
		}
		
		public function queryService (options:Object):void {
			seminar_id = options.seminarId;
			standort_id = options.standortId;
			_token = options.token;
			JsonProxy.appendToUrl = "&token=" + _token;
			
			if ( seminarLoader == null ) {
				seminarLoader = new JsonProxy("/admin/json;json/seminar/"+encodeURIComponent(seminar_id)+";json?standort_id=" + standort_id);
			} else {
				seminarLoader.setUrl ("/admin/json;json/seminar/" + encodeURIComponent(seminar_id) + "?standort_id=" + standort_id)
			}
			
			if ( standortLoader == null ) {
				standortLoader = new JsonProxy("/admin/json/standort;json/"+standort_id);		
			} else {
				standortLoader.setUrl ("/admin/json/standort;json/"+standort_id);
			}
			
			standortLoader.request(onStandort);
			seminarLoader.request(onJsonResult);
		}
		
		
		private function onStandort ( result:Object ):void {
			standort = result.data.name;
		}
		
		private function onJsonResult( result:Object ):void {
			GMBus.log("onJsonResult");
			tage = new Array();

			seminar_id = result.data.id;
			dauer = result.data.dauer;
			seminar = result.data;
			
			planungLoader = new JsonProxy("/admin/json;json/seminarArtReferent/" + encodeURIComponent(seminar_id) + "?standort_id=" + standort_id);
			planungLoader.request(onTagesplanungResult);
		}		
		
		private function onTagesplanungResult (result:Object):void {
			// initalize seminar 
			var i:int;
			
			//initalize global var Tage
		
			for ( i = 1; i <= parseInt(seminar.dauer); i++) {
				var tmpT:PlanungTag = new PlanungTag();
				var referentTag: Array = new Array();
				tmpT.tag = i;
				
				for ( var j:String in result.data[i] ) {
					referentTag.push(result.data[i.toString()][j]);
					GMBus.log( "Data:"+ result.data[i.toString()][j] );	
				}
				
				tmpT.data = new ArrayCollection(referentTag);
				tage.push(tmpT);
			}		
				
			p.tage = tage;
		}
	]]></mx:Script>
</mx:Application>